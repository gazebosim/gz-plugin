load(
    "//ign_bazel:generate_include_header.bzl",
    "generate_include_header",
)

load(
    "//ign_bazel:generate_file.bzl",
    "generate_file",
)

public_headers_no_gen = [
    "include/ignition/plugin/Register.hh",
    "include/ignition/plugin/RegisterMore.hh",
    "include/ignition/plugin/detail/Register.hh",
]

generate_file(
    name = "include/ignition/plugin/register/Export.hh",
    content = """
#pragma once
// IGN_DEPRECATED is defined by all ignition libraries, but the version below
// is a simplified version.  When mixing the regular ignition libraries and
// the drake compiled ignition libraries, the compiler throws a warning about
// the macro being multiply defined.  We undefine it before redefining it here
// to work around that issue.  Note that the IGNITION_PLUGIN_REGISTER_VISIBLE macro
// shouldn't be defined multiple times, but we undefine it just in case.
#ifdef IGNITION_PLUGIN_REGISTER_VISIBLE
#undef IGNITION_PLUGIN_REGISTER_VISIBLE
#endif
#define IGNITION_PLUGIN_REGISTER_VISIBLE __attribute__ ((visibility("default")))
#ifdef IGN_DEPRECATED
#undef IGN_DEPRECATED
#endif
#define IGN_DEPRECATED(version) __attribute__ ((__deprecated__))
    """,
    visibility = ["//visibility:private"],
)

generate_include_header(
    name = "register_hh_genrule",
    out = "include/ignition/plugin/register.hh",
    hdrs = public_headers_no_gen + [
        "include/ignition/plugin/register/Export.hh",
    ],
)

public_headers = public_headers_no_gen + [
    "include/ignition/plugin/register/Export.hh",
    "include/ignition/plugin/register.hh",
]

cc_binary(
    name = "libignition-plugin2-register.so",
    srcs = public_headers,
    includes = ["include"],
    linkopts = ["-Wl,-soname,libignition-plugin2-register.so"],
    linkshared = True,
    visibility = [],
    deps = [
        "//ign_plugin/core:ign_plugin",
        "//ign_bazel:utilities",
    ],
)

cc_library(
    name = "register",
    srcs = ["libignition-plugin2-register.so"],
    hdrs = public_headers,
    includes = ["include"],
    visibility = ["//visibility:public"],
    linkstatic = True,
    alwayslink = 1,
    deps = [
        "//ign_plugin/core:ign_plugin",
        "//ign_bazel:utilities",
    ],
)
